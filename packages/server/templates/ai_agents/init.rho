new rl(`rho:registry:lookup`),
    revAddress(`rho:rev:address`),
    devNull(`rho:io:devNull`),
    treeHashMapCh,
    listOpsCh,
    stackCh,
    getValueOr,
    visit
in {
    rl!(`rho:lang:treeHashMap`, *treeHashMapCh) |
    for(treeHashMap <- treeHashMapCh) {
        treeHashMap!("init", 3, *treeHashMapCh) |

        for(@map <- treeHashMapCh) {
            treeHashMapCh!(*treeHashMap, map)
        }
    } |

    rl!(`rho:lang:listOps`, *listOpsCh) |
    for(@(_, listOps) <- listOpsCh) {
        listOpsCh!(listOps)
    } |

    rl!(`rho:lang:stack`, *stackCh) |
    for(@(_, stack) <- stackCh) {
        stackCh!(stack)
    } |

    contract getValueOr(@map, @address, valueCh, nilCh) = {
        new retCh in {
            for(treeHashMap, _ <<- treeHashMapCh) {
                treeHashMap!("get", map, address, *retCh) |

                for(@value <- retCh) {
                    match value {
                        Nil => nilCh!()
                        value => valueCh!(value)
                    }
                }
            }
        }
    } |

    contract visit(@"agents", @deployerId, f, notFound) = {
        for(_, @map <<- treeHashMapCh) {
            getValueOr!(map, deployerId, *f, *notFound)
        }
    } |

    contract visit(@"agentVersions", @deployerId, @id, f, notFound) = {
        new foundCh in {
            visit!("agents", deployerId, *foundCh, *notFound) |

            for(@(agents, _) <- foundCh) {
                getValueOr!(agents, id, *f, *notFound)
            }
        }
    } |

    contract visit(@"agentVersion", @deployerId, @id, @version, f, notFound) = {
        new foundCh in {
            visit!("agentVersions", deployerId, id, *foundCh, *notFound) |

            for(@(agentVersions, _) <- foundCh) {
                getValueOr!(agentVersions, version, *f, *notFound)
            }
        }
    } |

    contract @"createAiAgent"(@deployerId, @id, @version, @name, @shard, @code) = {
        new deployerAddressCh, valueCh, nilCh, errCh in {
            revAddress!("fromDeployerId", deployerId, *deployerAddressCh) |

            for(@address <- deployerAddressCh) {
                for(@(agents, agentsIndex) <- valueCh; treeHashMap, _ <<- treeHashMapCh; stack <<- stackCh) {
                    new insertAgentCh, agentVersionsCh, agentVersionsIndexCh in {
                        for(<- insertAgentCh) {
                            stack!("push", agentsIndex, id, *devNull) |

                            treeHashMap!("init", 3, *agentVersionsCh) |
                            stack!("init", *agentVersionsIndexCh) |

                            for(
                                @agentVersions <- agentVersionsCh &
                                @agentVersionsIndex <- agentVersionsIndexCh
                            ) {
                                treeHashMap!("set", agents, id, (agentVersions, agentVersionsIndex), *devNull) |

                                treeHashMap!("set", agentVersions, version,
                                    {
                                        "name": name,
                                        "shard": shard,
                                        "code": code,
                                    }, *devNull) |

                                treeHashMap!("set", agentVersions, "latest",
                                    {
                                        "version": version,
                                        "name": name,
                                        "shard": shard,
                                        "code": code,
                                    }, *devNull) |

                                stack!("push", agentVersionsIndex, version, *devNull)
                            }
                        } |

                        getValueOr!(agents, id, *errCh, *insertAgentCh)
                    }
                } |

                for(<- nilCh; treeHashMap, @map <<- treeHashMapCh; stack <<- stackCh) {
                    new agentsCh, agentsIndexCh, agentVersionsCh, agentVersionsIndexCh in {
                        treeHashMap!("init", 3, *agentsCh) |
                        stack!("init", *agentsIndexCh) |

                        treeHashMap!("init", 3, *agentVersionsCh) |
                        stack!("init", *agentVersionsIndexCh) |

                        for(
                            @agents <- agentsCh &
                            @agentsIndex <- agentsIndexCh &
                            @agentVersions <- agentVersionsCh &
                            @agentVersionsIndex <- agentVersionsIndexCh
                        ) {
                            treeHashMap!("set", map, address, (agents, agentsIndex), *devNull) |

                            treeHashMap!("set", agents, id, (agentVersions, agentVersionsIndex), *devNull) |
                            stack!("push", agentsIndex, id, *devNull) |

                            treeHashMap!("set", agentVersions, version,
                                {
                                    "name": name,
                                    "shard": shard,
                                    "code": code,
                                }, *devNull) |

                            treeHashMap!("set", agentVersions, "latest",
                                {
                                    "version": version,
                                    "name": name,
                                    "shard": shard,
                                    "code": code,
                                }, *devNull) |

                            stack!("push", agentVersionsIndex, version, *devNull)
                        }
                    }
                } |

                for(<- errCh) {
                    Nil
                } |

                visit!("agents", address, *valueCh, *nilCh)
            }
        }
    } |

    contract @"listAiAgents"(@address, ret) = {
        new valueCh, nilCh in {
            for(@(agents, agentsIndex) <- valueCh; treeHashMap, _ <<- treeHashMapCh; listOps <<- listOpsCh; stack <<- stackCh) {
                new toAgentHeader, agentsListCh in {
                    contract toAgentHeader(@id, ret) = {
                        new agentContextCh, agentLastVersionCh in {
                            treeHashMap!("get", agents, id, *agentContextCh) |

                            for(@(agentVersions, _) <- agentContextCh) {
                                treeHashMap!("get", agentVersions, "latest", *agentLastVersionCh) |

                                for(@agentLastVersion <- agentLastVersionCh) {
                                    ret!(agentLastVersion.delete("code").set("id", id))
                                }
                            }
                        }
                    } |

                    stack!("toList", agentsIndex, *agentsListCh) |

                    for(@agentsList <- agentsListCh) {
                        listOps!("parMap", agentsList, *toAgentHeader, *ret)
                    }
                }
            } |

            for(<- nilCh) {
                ret!([])
            } |

            visit!("agents", address, *valueCh, *nilCh)
        }
    } |

    contract @"listAiAgentVersions"(@address, @id, ret) = {
        new valueCh, nilCh, agentVersionsListCh, toAgentHeader in {
            for(@(agentVersions, agentVersionsIndex) <- valueCh; treeHashMap, _ <<- treeHashMapCh; listOps <<- listOpsCh; stack <<- stackCh) {
                contract toAgentHeader(@version, ret) = {
                    new versionCh in {
                        treeHashMap!("get", agentVersions, version, *versionCh) |

                        for(@agentVersion <- versionCh) {
                            ret!(agentVersion.delete("code").union({"id": id, "version": version}))
                        }
                    }
                } |

                stack!("toList", agentVersionsIndex, *agentVersionsListCh) |

                for(@agentVersionsList <- agentVersionsListCh) {
                    listOps!("parMap", agentVersionsList, *toAgentHeader, *ret)
                }
            } |

            for(<- nilCh) {
                ret!(Nil)
            } |

            visit!("agentVersions", address, id, *valueCh, *nilCh)
        }
    } |

    contract @"getAiAgent"(@address, @id, @version, ret) = {
        new valueCh, nilCh in {
            for(@agentVersion <- valueCh) {
                ret!(agentVersion.union({"id": id, "version": version}))
            } |

            for(<- nilCh) {
                ret!(Nil)
            } |

            visit!("agentVersion", address, id, version, *valueCh, *nilCh)
        }
    } |

    contract @"saveAiAgent"(@deployerId, @id, @version, @name, @shard, @code) = {
        new deployerAddressCh, valueCh, errCh, insertAgentVersionCh in {
            revAddress!("fromDeployerId", deployerId, *deployerAddressCh) |

            for(@address <- deployerAddressCh) {
                for(@(agentVersions, agentVersionsIndex) <- valueCh; treeHashMap, _ <<- treeHashMapCh; stack <<- stackCh) {
                    for(<- insertAgentVersionCh) {
                        stack!("push", agentVersionsIndex, version, *devNull) |

                        treeHashMap!("set", agentVersions, version,
                            {
                                "name": name,
                                "shard": shard,
                                "code": code,
                            }, *devNull) |

                        treeHashMap!("set", agentVersions, "latest",
                            {
                                "version": version,
                                "name": name,
                                "shard": shard,
                                "code": code,
                            }, *devNull)
                    } |

                    getValueOr!(agentVersions, version, *errCh, *insertAgentVersionCh)
                } |

                for(<- errCh) {
                    Nil
                } |

                visit!("agentVersions", address, id, *valueCh, *errCh)
            }
        }
    }
}
